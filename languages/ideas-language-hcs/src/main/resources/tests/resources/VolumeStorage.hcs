Service SimpleBlockStorage {
	%SelectableTerms
	Size: int [1, 5];
	SSD: boolean;
	Region: enum {"USA" , "EU" , "JP"};

	%DerivedTerms
	costGBMonth: real [0.00, 0.15]; // euros/GB per month
	volumeCostMonth: real [0.00, 50.0] ; // euros per month

	%Dependencies
	//pricing
	volumeCostMonth == costGBMonth * Size;
	
	 table (Region, SSD -> costGBMonth) {
		"USA", true -> 0.1;
		"EU", true -> 0.12;
		"JP", true -> 0.15;
		"USA", false -> 0.05;
		"EU", false -> 0.06;
		"JP", false -> 0.08;
	}
}

Highly_configurable Service VolumeStorage {
	%Services
	storage: SimpleBlockStorage [1,*];
	
	%GlobalTerms
	totalCostMonth : real [0.00, 10000.00];
	discount: real [0.00, 1000.00];
	
	%Dependencies
	// cost aggregation
	totalCostMonth == sum(storage.volumeCostMonth);
	// discount policy
	totalCostMonth >= 3000 -> discount == totalCostMonth * 0.1;
}